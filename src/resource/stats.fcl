FUNCTION_BLOCK stats

    VAR_INPUT
        kill: REAL;
        assist: REAL;
        death: REAL;
        kast: REAL;
    END_VAR

    VAR_OUTPUT
        performance: REAL;
    END_VAR

    FUZZIFY kill
        //number of kills in one map played
        TERM low := (0, 1) (10, 0);
        TERM medium := (8, 0) (15, 1) (20, 0);
        TERM high := (18, 0) (30, 1) (50, 1);
    END_FUZZIFY

    FUZZIFY assist
        //number of assists in one map played
        TERM low := trian 0 2 5;
        TERM medium := trian 6 7 9;
        TERM high := (10, 0) (13, 1) (15, 1);
    END_FUZZIFY

    FUZZIFY death
        //number of assists in one map played
        // higher is better
        TERM high := trian 0 5 10;
        TERM medium := trian 10 14 18;
        TERM low := (18, 0) (25, 1) (30, 1);
    END_FUZZIFY

    FUZZIFY kast
        /**
        *KAST percentage per round in wich the player either had
        *for KILLS, ASSISTS, SAVE ROUNDS and TRADED KILLS
        **/

        TERM low := trape 0 20 40 60;
        TERM medium := trian 60 73 82.5;
        TERM high := trian 83 92 100;
    END_FUZZIFY

    DEFUZZIFY performance
        TERM bad := trian 0 2 4;
        TERM average := trian 4 5.5 7;
        TERM good := (7, 0) (8, 1) (10, 1);
        METHOD : COG;
        DEFAULT := 0;
    END_DEFUZZIFY

    RULEBLOCK No1
        AND : MIN;
        ACT : MIN;
        ACCU : MAX;


        //bad rules
            RULE 1 : IF kill IS low AND assist IS low AND death IS low AND kast IS low THEN performance IS bad;
            RULE 2 : IF kill IS low AND assist IS medium AND death IS low AND kast IS low THEN performance is bad;
            RULE 3 : IF kill IS low AND assist IS low AND death IS medium AND kast IS low THEN performance is bad;

        //average rules
            RULE 4 : IF kill IS medium AND assist IS medium AND death IS medium AND kast is medium THEN performance IS average;
            RULE 5 : IF kill IS high AND assist IS medium AND death IS medium AND kast IS medium THEN performance IS average;
            RULE 6 : IF kill IS high AND assist IS medium AND death IS medium AND kast IS high THEN performance IS average;
            RULE 7 : IF kill IS high AND assist IS medium AND death IS low AND kast IS medium THEN performance IS average;

        //good rules
            RULE 8 : IF kill IS high AND assist IS high AND death IS high AND kast IS high THEN performance IS good;
            RULE 9 : IF kill IS high AND assist IS low AND death IS medium AND kast IS high THEN performance IS good;
    END_RULEBLOCK
END_FUNCTION_BLOCK